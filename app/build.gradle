plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion as Integer

    defaultConfig {
        applicationId "com.github.hiteshsondhi88.sampleffmpeg"
        minSdkVersion rootProject.ext.minSdkVersion as Integer
        targetSdkVersion rootProject.ext.targetSdkVersion as Integer
        versionCode getGitCommitCount()
        versionName getTag()
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets.main {
        assets.srcDirs = ['assets']
        jniLibs.srcDirs = ['libs']
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/services/javax.annotation.processing.Processor']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    implementation project(':FFmpegAndroid')

    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
}

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}

static def getTag() {
    def process = "git describe --tags".execute()
    return process.text.toString().trim()
}